/*************************************************************************  
* 프로시저명: dbo.SP_DBA_CHECK_DATA_COMPARE_SCRIPT
* 작성정보	: 2013-03-26 BY Seo Eun Mi
* 관련페이지:  
* 내용		: 테이블 이관 후 최신 데이터 위주로 컬럼별 데이터 비교 스크립트 만들어주는 SP
* 수정정보	:
**************************************************************************/
CREATE PROCEDURE [dbo].[SP_DBA_CHECK_DATA_COMPARE_SCRIPT]
	@SOURCE_DB_NAME		SYSNAME,	-- 변경전 비교 기준 DB명
	@SOURCE_DB_SCHEMA	SYSNAME,	-- 변경전 비교 스키마
	@SOURCE_TABLE_NAME	SYSNAME,	--변경전 비교 기준이 되는 테이블명
	@TARGET_DB_NAME		SYSNAME,	--변경후 비교 대상 DB명
	@TARGET_TABLE_NAME	SYSNAME,	--변경후 비교 대상이 되는 테이블명
	@SAMPLE_PERCENT INT = 5  --비교데이터 샘플링 퍼센트

AS
BEGIN
	SET NOCOUNT ON
	
	DECLARE @CLUSTER_INDEX_NAME VARCHAR(500), @COUNT INT, @SOURCE_FULL_OBJECT SYSNAME, @TARGET_FULL_OBJECT SYSNAME
	DECLARE @IDX INT, @SQL NVARCHAR(4000), @FULL_SQL NVARCHAR(4000)
	DECLARE @dbcc VARCHAR(500), @rowcount INT
	DECLARE @COLUMN_NAME VARCHAR(100), @COLUMN_TYPE VARCHAR(100), @COLUMN_VALUE VARCHAR(100) 
	DECLARE @STR_SQL NVARCHAR(4000), @STR_PARM NVARCHAR(1000), @MAX_IDX INT, @SAMPLE_IDX INT
	SET @max_idx = 0

	IF @TARGET_DB_NAME IS NULL SET @TARGET_DB_NAME = @SOURCE_DB_NAME
	IF @TARGET_TABLE_NAME IS NULL SET @TARGET_TABLE_NAME = @SOURCE_TABLE_NAME + '_ETAM'

	SET @SOURCE_FULL_OBJECT = @SOURCE_DB_NAME + '.' + @SOURCE_DB_SCHEMA+ '.' + @SOURCE_TABLE_NAME
	SET @TARGET_FULL_OBJECT = @TARGET_DB_NAME + '.' + @SOURCE_DB_SCHEMA+ '.' + @TARGET_TABLE_NAME

	DECLARE @object_id int
	SET @object_id = object_id(@SOURCE_FULL_OBJECT)
	IF @object_id IS NULL 
	BEGIN
		PRINT '/******** NOT Find Object ************/'
		RETURN
	END

	SET @COLUMN_VALUE = 1
	SET @COUNT = 0
	SET @IDX = 1

	SET @STR_SQL = 'SELECT TOP 1 @CLUSTER_INDEX_NAME = A.NAME ' + CHAR(10)
				 + ', @COLUMN_NAME = B.NAME,	@COLUMN_TYPE = D.name ' + CHAR(10)
				 + 'FROM ' + @SOURCE_DB_NAME + '.sys.INDEXES A (NOLOCK)' + CHAR(10)
				 + 'LEFT JOIN ' + @SOURCE_DB_NAME + '.sys.index_columns C (NOLOCK) ON A.object_id = C.object_Id  ' + CHAR(10)
				 + ' AND A.index_id = C.index_id ' + CHAR(10)
				 + 'LEFT JOIN ' + @SOURCE_DB_NAME + '.sys.COLUMNS B (NOLOCK) ON A.object_id = B.object_id AND C.column_id = B.column_id' + CHAR(10)
				 + 'LEFT JOIN ' + @SOURCE_DB_NAME + '.sys.types D (NOLOCK) ON B.user_type_id = D.user_type_id ' + CHAR(10)
				 + 'WHERE A.OBJECT_ID = OBJECT_ID(''' + @SOURCE_FULL_OBJECT + ''') AND A.INDEX_ID  = 1 ORDER BY C.key_ordinal '

	SET @STR_PARM = N'@CLUSTER_INDEX_NAME SYSNAME OUTPUT, @COLUMN_NAME SYSNAME OUTPUT, @COLUMN_TYPE SYSNAME OUTPUT'
	--PRINT @STR_SQL
	EXECUTE SP_EXECUTESQL @STR_SQL, @STR_PARM, @CLUSTER_INDEX_NAME = @CLUSTER_INDEX_NAME OUTPUT, 
			@COLUMN_NAME = @COLUMN_NAME OUTPUT, @COLUMN_TYPE = @COLUMN_TYPE OUTPUT

	
	
	CREATE TABLE #TEMP_MIGRATION_ROWRANGE
	(
		   IDX INT IDENTITY(1,1)
	,      RANGE_HI_KEY VARCHAR(200)
	,      RANGE_ROWS numeric(20,1)
	,      EQ_ROWS  numeric(20,6)
	,      DISTINCT_RANGE_ROWS bigint
	,      AVG_RANGE_ROWS numeric(20,6)
	)

	


	
	SET @dbcc = 'USE ' + @SOURCE_DB_NAME + CHAR(10)
			  + 'DBCC show_statistics (' + @SOURCE_TABLE_NAME + ', ' + @CLUSTER_INDEX_NAME + ') WITH HISTOGRAM , NO_INFOMSGS '
	

	INSERT #TEMP_MIGRATION_ROWRANGE
	EXEC(@DBCC)
	
	SELECT @MAX_IDX = max(idx) FROM #TEMP_MIGRATION_ROWRANGE	
		
	SET @SAMPLE_IDX = (@MAX_IDX * @SAMPLE_PERCENT) / 100
	
	SELECT TOP 1 @COLUMN_VALUE = RANGE_HI_KEY FROM #TEMP_MIGRATION_ROWRANGE WHERE IDX >=@MAX_IDX-@SAMPLE_IDX ORDER BY IDX 
	
	--SELECT @COLUMN_NAME,@COLUMN_TYPE,@COLUMN_VALUE,@SAMPLE_PERCENT, @MAX_IDX-@SAMPLE_PERCENT
	--SELECT * FROM #TEMP_MIGRATION_ROWRANGE
	
	IF @COLUMN_NAME IS NULL 
		PRINT 'script 추출 에러 발생! sp 확인-[SP_DBA_CHECK_DATA_COMPARE_SCRIPT]'
	
	
	SET @STR_SQL = ''
	
	
	 DECLARE @STR_SQL1 Nvarchar(4000), @STR_SQL2 Nvarchar(4000), @STR_SQL3 Nvarchar(4000)
	 SET @STR_SQL2 = ''

	 
	 SET @STR_SQL1 = N'SELECT  @STR_SQL2 =  STUFF(( SELECT  '' AND  isnull(src.'' +  name + '' ,1)''  + '' = ' +  'isnull(dest.'' +  name  + '' ,1) ''' 
	 SET @STR_SQL1 = @STR_SQL1 + N' FROM @DB.sys.columns with (nolock) '
	 SET @STR_SQL1 = @STR_SQL1 + N' WHERE  object_id = @object_id   FOR XML PATH('''')),1,1,'''')' 

	 SET @STR_SQL1 = REPLACE(@STR_SQL1 ,'@DB',@SOURCE_DB_NAME)
	 SET @STR_SQL1 = REPLACE(@STR_SQL1,'@object_id',@object_id)

	 EXEC SP_EXECUTESQL @STR_SQL1, N'@STR_SQL2 nvarchar(4000) output', @STR_SQL2 = @STR_SQL2 OUTPUT
	 		
	 SET @STR_SQL3 = CHAR(10) + 'WHERE DEST.' + @COLUMN_NAME + ' IS NULL ' 
	 
	 IF @SAMPLE_PERCENT < 100
	 BEGIN
		
		SET @STR_SQL3 = @STR_SQL3 + CHAR(10) + 'AND SRC.' + @COLUMN_NAME  + ' >= ' 
		SET @STR_SQL3 = @STR_SQL3 + CASE WHEN @COLUMN_TYPE IN ('INT', 'BIGINT') THEN @COLUMN_VALUE ELSE ''''+ @COLUMN_VALUE + '''' END
	 END
	 
	
	 SET @STR_SQL = N''  + CHAR(10) 
	    + '' + 
	    + 'SELECT SRC.@COLUMN_NAME  INTO #TMP FROM  @SOURCE_FULL_OBJECT SRC WITH(NOLOCK)'  + CHAR(10) 
		+ ' LEFT JOIN @TARGET_FULL_OBJECT  DEST WITH(NOLOCK)  ON 1=1 '   + CHAR(10) 
		+ @STR_SQL2
		+ @STR_SQL3


	 SET @STR_SQL = REPLACE(@STR_SQL,'@COLUMN_NAME',@COLUMN_NAME)
	 SET @STR_SQL = REPLACE(@STR_SQL,'@SOURCE_FULL_OBJECT',@SOURCE_FULL_OBJECT)
	 SET @STR_SQL = REPLACE(@STR_SQL,'@TARGET_FULL_OBJECT',@TARGET_FULL_OBJECT)
	 SET @STR_SQL = REPLACE(@STR_SQL,'@COLUMN_TYPE',@COLUMN_TYPE)
	 SET @STR_SQL = REPLACE(@STR_SQL,'@COLUMN_VALUE',@COLUMN_VALUE)

	 PRINT @STR_SQL
	 PRINT ''
	 PRINT 'SELECT * FROM #TMP '
	 PRINT ''


END


